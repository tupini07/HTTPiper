// program
program = { SOI ~ (import  | var_assignment | request_signature | NEWLINE)+ ~ EOI}

// rules for assignments
var_assignment = { variable_id ~ "=" ~ value }

// rules for import
import = ${"import" ~ WHITESPACE+ ~ "\"" ~ filename ~ "\""}
filename = { (ASCII_ALPHA | ASCII_DIGIT)+ ~ "." ~ (ASCII_ALPHA | ASCII_DIGIT)+ }

// rules for request definition
request_signature = {(requestId ~ NEWLINE)? ~ 
					method ~ url ~ 
                    (NEWLINE ~ headers)? ~  
                    (NEWLINE{2} ~ body)? ~ 
                    (NEWLINE+ | EOI)}

headers = { header ~ (NEWLINE ~ header)* }
header = { key ~ value }

body = {json}

key = @{key_value_char+}
value = {substitutable_value+}

url = {substitutable_value}
requestId = _{"[" ~ request_id ~ "]"}

text = { (ASCII_ALPHA | " " | "?" | "&" | "=")+ }

method = {"GET" | "POST" | "PATCH"}

substitution = { "{{" ~ (variable_id | reqAttribute | empty) ~ (">" ~ pipes)? ~ "}}" }
empty = {"''"}
reqAttribute = {!"@" ~ request_id ~ "." ~ req_resp ~ "." ~ body_headers}
req_resp = {"Request" | "Response"}
body_headers = {"Body" | "Headers"} 
pipes = {command ~ ("|" ~ command)*}
command = {(word | "\"" | "'" | "-")+}

request_id = @{ASCII_ALPHA_LOWER ~ ASCII_ALPHA*}
variable_id = @{"@" ~ ASCII_ALPHA+}


// json/body specific rules
jsonpair = { key ~ ":" ~ jsonvalues_possibilities }
jsonobject = {"{" ~ NEWLINE* ~ "}" | 
			  "{" ~ NEWLINE* ~ jsonpair ~ ("," ~ NEWLINE* ~ jsonpair)* ~ NEWLINE* ~ "}"}
jsonarray = {"[" ~ NEWLINE* ~ "]" | 
			 "[" ~ NEWLINE* ~ jsonvalues_possibilities ~ ("," ~ NEWLINE* ~ jsonvalues_possibilities)* ~ NEWLINE* ~ "]"}


// below are non-printable rules
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

substitutable_value = _{(substitution | text)+}
word = _{ASCII_ALPHA+}
key_value_char = _{ ASCII_ALPHA 
                    | ASCII_DIGIT 
                    | "_" | "-" | "?" | "&" | "=" }
json = _{jsonobject | jsonarray}
jsonvalues_possibilities = _{jsonobject | jsonarray | value}
